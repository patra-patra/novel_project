label school_day1:
    $ days_worked["school"] += 1

    $ day = day + 1

    scene bg director_office with fade
    
    "Кабинет был погружен в полумрак, несмотря на день за окном. Массивный стол, заставленный бумагами, пах старым деревом и строгостью."
    "За ним, как монумент, сидела Валентина Сергеевна, директриса. Ее взгляд, тяжелый и оценивающий, скользнул по мне с головы до ног."
    
    show sveta nervous at center with dissolve
    "От чего-то я чувствовала себя как на экзамене. Валентина Сергеевна разглядывала мой наряд, будто бы я была инопланетным артефактом."
    "Она разглядывала мою трудовую книжку, где за выпуском из пединститута зияла трёхлетняя пустота."
    
    show director serious at right with dissolve
    v "Так-с... Светлана Олеговна. Окончила... с грехом пополам. А потом... творческий отпуск?"
    "Голос у нее был низкий, без единой нотки одобрения."
    
    s "Ну, вы знаете... Сложились обстоятельства."
    
    v "Обстоятельства. У нас тут, милочка, не салон красоты. И не ночной клуб. У нас дети."
    v "Вы хоть помните, как выглядит учебник по английскому? Или пять лет только журналы «Космополитен» читали?"
    
    s "Конечно помню! Я все помню. И Капитанскую дочку, и Грибоедова... да даже Колобка!"
    
    "Валентина Сергеевна откладывает трудовую, складывает руки на столе."
    
    v "Светлана Олеговна, вы не учитель литературы. Вы же пришли преподавать английский."
    
    s "А... Ой, хихи. Ну паст симплы это ваще легкотня!"
    
    "Валентина Сергеевна помолчала, голос смягчается на полтона, в нем появляется усталая, житейская нота."
    
    v "У нас тут полкласса — дети с трудной судьбой. Отцы пьют, матери на двух работах."
    v "Им нужна не кукла из журнала. Им нужна... наставник. Хоть какой-то."
    v "Сможете ли вы им быть? Или при первой же трудности сбежите обратно в свои «обстоятельства»?"
    
    "Руки сжались так, что костяшки побелели. В горле стоял ком. Унижение и злость смешались с отчаянным желанием доказать."
    
    s "Ни за что! Я никуда не сбегу! Мне... мне некуда бежать. Дайте мне шанс. Я все выучу. Я буду работать. Я не подведу… Честное слово!"
    
    "Валентина Сергеевна смотрела на меня еще долгих десять секунд. Затем она тяжело вздыхает и берёт ручку."
    
    v "Ладно. Возьму вас на испытательный срок. Месяц. Шестой «Б». Они вас... протестируют."
    "Она снова смотрит на меня, и в углу ее глаза чуть заметно дрогнуло."
    v "И, Светлана Олеговна..."
    
    s "Да?"
    
    v "Смените туфли на каблуках на что-нибудь человеческое. У нас тут коридоры скользкие. И дети быстрые. Догонять придется."
    
    menu:
        "Как насчёт кроссовок?":
            $ works_score["school"] -= 1
            s "Ой, а я как раз в «Спортмастере» новые кроссовки видела! Розовые, с блестками. Они подойдут? Для педагогического образа?"
            
            "Валентина Сергеевна просто закрыла глаза на секунду, как будто прося сил у высших сил."
            
            v "Просто каблук поменьше. А лучше без него."
            
            s "Ну блин… Ладно."
            
        "Это мои боевые доспехи":
            $ works_score["school"] += 1
            s "Валентина Сергеевна, это не просто каблуки. Это мои боевые доспехи."
            s "В этих туфлях я на двадцать сантиметров выше, а значит — на двадцать сантиметров ближе к тому, чтобы меня воспринимали всерьёз."
            s "Я знаю, как я выгляжу. Но дайте мне шанс доказать, что обложка не всегда соответствует содержанию."
            s "Я обещаю, что буду бегать за ними в этих каблуках так же быстро, как они будут убегать от домашнего задания."
            s "А если поскользнусь — встану и догоню. Без жалоб."
            
            "Валентина Сергеевна хмыкает, откладывая ручку."
            
            v "Ладно. Оставляйте свои... двадцать сантиметров уверенности."
            v "Посмотрим, выдержат ли ваши «доспехи» скользкий линолеум."
    
    "Валентина Сергеевна поставила подпись в заявлении."
    
    show sveta happy at center
    "Настал мой шанс проявить себя как самая лучшая училка Норильска!"
    
    hide sveta
    hide director
    with dissolve

    call screen room_evening